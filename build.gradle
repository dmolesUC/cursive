buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'org.junit.platform:junit-platform-gradle-plugin:1.1.0'
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.6'
}

subprojects {
  apply plugin: 'org.junit.platform.gradle.plugin'

  sourceCompatibility = 9
  targetCompatibility = 9

  repositories {
    jcenter()
  }

  ext {
    vavrVersion = '0.9.2'

    slf4jVersion = '1.7.25'

    junitVersion = '5.1.0'

    mockitoVersion = '2.16.0'
    assertJVersion = '3.9.1'

    nettyVersion = "4.1.22.Final"
  }

  configurations.all {
    resolutionStrategy.eachDependency { details ->
      // Make sure Vert.x and TinkerPop don't import incompatible versions
      // of different parts of the Netty stack
      if (details.requested.group == 'io.netty') {
        details.useVersion(nettyVersion)
      }
    }
  }

  dependencies {

    // ------------------------------------------------------------
    // Apache Commons

    compile 'commons-io:commons-io:2.6'
    compile 'org.apache.commons:commons-text:1.1'

    // ------------------------------------------------------------
    // Vavr

    compile "io.vavr:vavr:${vavrVersion}"

    // ------------------------------------------------------------
    // Logging

    compile "org.slf4j:slf4j-api:$slf4jVersion"
    testCompile "org.slf4j:slf4j-simple:$slf4jVersion"

    // ------------------------------------------------------------
    // Testing

    testCompile "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testCompile "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testCompile "org.junit.vintage:junit-vintage-engine:$junitVersion"

    testCompile "org.mockito:mockito-core:$mockitoVersion"
    testCompile "org.assertj:assertj-core:$assertJVersion"
  }
}
