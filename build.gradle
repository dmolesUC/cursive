buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.2.1'
}

subprojects {
  apply plugin: 'org.junit.platform.gradle.plugin'

  sourceCompatibility = 9
  targetCompatibility = 9

  repositories {
    jcenter()
  }

  ext {
    vavrVersion = '0.9.2'

    slf4jVersion = '1.7.25'

    junitJupiterVersion = '5.0.2'
    junitVintageVersion = '4.12.2'

    mockitoVersion = '2.13.0'
    assertJVersion = '3.8.0'

    nettyVersion = "4.1.19.Final"
  }

  configurations.all {
    resolutionStrategy.eachDependency { details ->
      // Make sure Vert.x and TinkerPop don't import incompatible versions
      // of different parts of the Netty stack
      if (details.requested.group == 'io.netty') {
        details.useVersion nettyVersion
      }
    }
  }

  dependencies {

    // ------------------------------------------------------------
    // Apache Commons

    compile 'commons-io:commons-io:2.6'
    compile 'org.apache.commons:commons-text:1.1'

    // ------------------------------------------------------------
    // Vavr

    compile "io.vavr:vavr:${vavrVersion}"

    // ------------------------------------------------------------
    // Logging

    compile "org.slf4j:slf4j-api:$slf4jVersion"
    testCompile "org.slf4j:slf4j-simple:$slf4jVersion"

    // ------------------------------------------------------------
    // Testing

    testCompile "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
    testCompile "org.junit.jupiter:junit-jupiter-params:$junitJupiterVersion"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion"
    testCompile "org.junit.vintage:junit-vintage-engine:$junitVintageVersion"

    // TODO: when JUnit 5.1.0 ships, remove apiguardian dependency
    // https://github.com/junit-team/junit5/issues/1104
    testCompile 'org.apiguardian:apiguardian-api:1.0.0'

    testCompile "org.mockito:mockito-core:$mockitoVersion"
    testCompile "org.assertj:assertj-core:$assertJVersion"

  }
}
